pipeline GoodsTransportPipeline {
    GoodsTransportExtractor 
        -> GoodsTransportTextFileInterpreter
        -> GoodsTransportTextRangeSelector
        -> GoodsTransportCSVInterpreter
        -> GoodsTransportHeaderWriterAtoE
        -> GoodsTransportHeaderWriterATtoAU
        -> GoodsTransportTableInterpreter
        -> GoodsTransportLoader;
    
    block GoodsTransportExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
    }

    block GoodsTransportTextFileInterpreter oftype TextFileInterpreter { encoding: "latin3"; }

    block GoodsTransportTextRangeSelector oftype TextRangeSelector {
        lineFrom: 8;
        lineTo: 41336;
    }

    block GoodsTransportCSVInterpreter oftype CSVInterpreter {
        enclosing: '';
        delimiter: ";";
    }
    
    block GoodsTransportHeaderWriterAtoE oftype CellWriter {
        at: range A1:E1;
        write: ["year", "month", "goods_id", "goods_name", "goods_source"];
        
    }

    block GoodsTransportHeaderWriterATtoAU oftype CellWriter{
        at: range AT1:AU1;
        write: ["abroad","total"];
    }

    valuetype PositiveIntegers oftype integer{
        constraints: [ RangePositiveInteger];
    }

    constraint RangePositiveInteger oftype RangeConstraint{
        lowerBound: 0;
        lowerBoundInclusive: false;
    }

    valuetype GermanMonths oftype text{
        constraints: [GermanMonthsList];
    }

    constraint GermanMonthsList oftype AllowlistConstraint {
        allowlist: ["Januar","Februar","MÃ¤rz","April","Mai","Juni","Juli","August","September","Oktober","November","Dezember"];
    }

    valuetype GoodsId oftype text{
        constraints: [RegGoodsId];
    }

    constraint RegGoodsId oftype RegexConstraint{
        regex: /^NST7-[0-9A-Z]{3}$/;
    }


    block GoodsTransportTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype PositiveIntegers,
            "month" oftype GermanMonths,
            "goods_id" oftype GoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveIntegers,
            "total" oftype PositiveIntegers,
        ];
    }

    block GoodsTransportLoader oftype SQLiteLoader {
        table: "goods";
        file: "goodsTransportedByTrain.sqlite";
    }
}
